grammar de.jpwinkler.daf.workflowdsl.WorkflowDsl with org.eclipse.xtext.common.Terminals

generate workflowDsl "http://www.jpwinkler.de/daf/workflowdsl/WorkflowDsl"

WorkflowModel:
	(elements+=WorkflowElement)*
	;

WorkflowElement:
	Target | Step | ModuleSet | Variable
;

Target:
	'target' step=[Step]
;

ModuleSet:
	'moduleset' name=ID '{'
	(moduleSetEntries+=ModuleSetEntry)*
	'}'
;

MODULE_SET_ENTRY_TYPE:
	'csv' | 'doors' | 'doorsurl' | 'csvfolder'
;

ModuleSetEntry:
	type=MODULE_SET_ENTRY_TYPE reference=STRING
;

Variable:
	SimpleVariable | ArrayVariable
;

SimpleVariable:
	'var' name=ID '=' value=STRING
;

ArrayVariable:
	'var' name=ID '[]' '=' '{' (items+=STRING ("," items+=STRING)*)? '}'
;

Step:
	ModelConstructorStep | ModelOperationStep
;

ModelConstructorStep:
	'constructor' name=ID '{'
	(features+=OperationFeature)*
	'}'
;

ModelOperationStep:
	'op' name=ID '{'
	(features+=OperationFeature)*
	'}'
;

OperationFeature:
	DependencyFeature | ImplementationFeature | ForFeature | SourceFeature
;

SourceFeature:
	'source' moduleSet=[ModuleSet]
;

ForFeature:
	'for' loopVar=ID 'in' arrayVar=ID '{'
		(features+=OperationFeature)*
	'}'
;

DependencyFeature:
	'dependency' step=[Step]
	('{'
		(variables+=Variable)*
	'}')?
	(name=ID)?
;

ImplementationFeature:
	'implementation'
	implementation=STRING
;