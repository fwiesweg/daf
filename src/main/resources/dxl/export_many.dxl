Skip createModuleSkip() {
	Regexp r = regexp2("(.*);(.*)")
	Skip result = create

	Stream s = read("$$moduleListFile$$")
	string line
	while (!end(s)) {
		s >> line
		if (r line) {
			put(result, line[match 1], line[match 2])
		} else {
			error "Could not match line " line
		}
	} 
	close s
	
	return result
}

string exportCsv(Module m, string moduleListKey) {
	string folder = "$$targetFolder$$"
	if (null create folder) {
		mkdir folder
	}
	string file = folder "\\" moduleListKey "_" name(m) ".csv"
	exportModuleToCSV(m, file)
	exportModuleMetaData(m, file ".mmd", moduleListKey)
	return "ok"
}

Skip modules = createModuleSkip()

string moduleUrl

for moduleUrl in modules do {
	string moduleListKey = (string key modules)
	
	print "Exporting " moduleListKey "... "
	
	string modname
	if (startsWith(moduleUrl, "doors://")) {
		modname = getModuleNameFromUrl(moduleUrl)
	} else {
		modname = moduleUrl
	}
	if (!null modname) {
		Module m = read(modname, false)
		if (!null m) {
			current = m
		
			load view "Standard View"
			
			print exportCsv(m, moduleListKey) "\n"
			close m
		} else {
			print "module not found\n"
		}
	} else {
		print "module not found\n"
	}
}

delete modules